#!/usr/bin/env python

Import('env')

env.drivers_sources = []
env.drivers_headers = []

# OS drivers
SConscript('unix/SCsub')
SConscript('windows/SCsub')

# Sounds drivers
SConscript('alsa/SCsub')
SConscript('coreaudio/SCsub')
SConscript('pulseaudio/SCsub')
if (env["platform"] == "windows"):
    SConscript("wasapi/SCsub")
if env['xaudio2']:
    SConscript("xaudio2/SCsub")

# Midi drivers
SConscript('alsamidi/SCsub')
SConscript('coremidi/SCsub')
SConscript('winmidi/SCsub')

# Graphics drivers
if (env["platform"] != "server"):
    SConscript('gles3/SCsub')
    SConscript('gles2/SCsub')
    SConscript('gl_context/SCsub')
else:
    SConscript('dummy/SCsub')

# Core dependencies
SConscript("png/SCsub")

if env['vsproj']:
    import os
    path = os.getcwd()
    # Change directory so the path resolves correctly in the function call.
    os.chdir("..")
    env.AddSourcesToVSProject(env.drivers_sources)
    env.AddHeadersToVSProject(env.drivers_headers)
    os.chdir(path)

env.add_source_files(env.drivers_sources, "*.cpp")
env.add_header_files(env.drivers_headers, "*.h")

lib = env.add_library("drivers", env.drivers_sources)
env.Prepend(LIBS=[lib])
